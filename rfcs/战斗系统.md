# 功能

- 战斗系统
- 开始日期
- 被提名的负责人

## 概述

战斗是 rpg 游戏最常见的设计。也是玩家主要的游玩内容。一个战斗系统可以多方面的侧重，一个是机制，讲究策略，一个动作，讲究操作，又或者兼二有之。

## 指南级别的解释

一个以机制为主的策略游戏。战斗系统的要素

- 发生的对象
  - 施加方
  - 承受方
- 获胜条件是什么
  - 某一方死亡

战斗系统的主要流程:

- 某人触发起了一次战斗。
- 交战双方进行一个先攻判定。
- 先攻的人进行一场攻击判定
  - 是否命中
    - 是否闪避
    - 是否豁免
  - 计算伤害信息
    - 计算基础伤害
    - 计算暴击伤害
    - 计算抗性
    - 计算抵挡
- 进行攻击结算
  - 进行伤害结算
  - 是否退出战斗
- 后攻击的人进行一个攻击判断
- 进行攻击结算
- 退出战斗

## 参考级别解释

判定器的功能是判断施加方是否可以命中承受方。定义一个类型 DodgeDecision 来完成这个功能，如下:

```gds
extends RefCounted
class_name Decision

# 本次判定的难度
var dc: int = 0

# 本次判定的基础数值
var base: int = 0

var factors: Array[DecisionFactor] = []

func decision() -> DicisionResult:
	var res = DicisionResult.new()
	var decision_factor_results = []
	var factor_sum = 0
	for factor in factors:
		var factor_res = factor.get_result()
		decision_factor_results.push_back(factor_res)
		factor_sum += factor_res.value

	var target_dc = base + factor_sum

	if target_dc >= dc:
		res.success = true
	else:
		res.success = false

	res.base = base
	res.dc = dc
	res.target_dc = target_dc
	res.decision_factor_results = decision_factor_results
	return res

```

DecisionFactor 是表示收到影响的影响因子。定义如下:

```gds
extends RefCounted
class_name DecisionFactor

enum FactorType {Number, Dice}

# 数值
var number: int
# 骰子
var dice: DndDice
# 影响因子的因素
var factor_type: DecisionFactor.FactorType

# 来源
var source: Node

func get_result() -> DecisionFactorResult:
	var res = DecisionFactorResult.new()
	res.factor_type = factor_type
	res.source = source

	match factor_type:
		FactorType.Number:
			res.value = number
			res.number_result = number
		FactorType.Dice:
			var dice_res = dice.get_result()
			res.value = dice_res.value
			res.dice_result = dice_res

	return res

class DecisionFactorResult:
	var value: int
	var source: Node
	var factor_type: DecisionFactor.FactorType
	var number_result: int
	var dice_result: DndDice.DiceResult

```

定义一个类型获取判定器要收集的所有数据，定义如下:

```gds
extends RefCounted

class_name DicisionResult

# 是否成功
var success: bool
# 本次判定的难度
var dc: int = 0
# 判断值
var target_dc: int = 0
# 本次判定的基础数值
var base: int = 0
var decision_factor_results: Array[DecisionFactor.DecisionFactorResult] =[]

```
